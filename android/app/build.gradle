def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { stream ->
        localProperties.load(stream)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException('Flutter SDK not found. Define location with flutter.sdk in the local.properties file.')
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def loadSigningConfiguration(String name) {
    def propertiesFile = file("signing/${name}.properties") as File
    if (!propertiesFile.exists()) {
        throw new FileNotFoundException("""The properties file ${propertiesFile.absolutePath} is missing, 
did you remember to add the signing configuration to the project for $name?

Please refer to the readme for further instructions.""")
    }

    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return properties
}

def getKeystore(String fileName) {
    def keystoreFile = file("signing/${fileName}") as File
    if (!keystoreFile.exists()) {
        throw new FileNotFoundException("""The keystore file ${keystoreFile.absolutePath} is missing, 
but the signing configuration for the current build variant specifies it as the storeFile.
Did you remember to add the keystore to the project for this build variant?

Please refer to the readme for further instructions.""")
    }
    return keystoreFile
}

def isSigningConfigurationDefinedFor(String name) {
    return file("signing/${name}.properties").exists()
}

def reportMissingSigningConfiguration(String name) {
    logger.error("""

⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️    
The signing configuration for Android $name builds is not defined,
please refer to the readme for further informations on how to set it up.
⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  ⚠️  

""")
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId 'com.yourcompany.dartconf'
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName '1.0'
    }

    signingConfigs {
        debug {
            def configuration = loadSigningConfiguration('debug')
            storeFile getKeystore(configuration['storeFile'])
            storePassword configuration['storePassword']
            keyAlias configuration['keyAlias']
            keyPassword configuration['keyPassword']
        }

        release {
            if (isSigningConfigurationDefinedFor('release')) {
                def configuration = loadSigningConfiguration('release')
                storeFile getKeystore(configuration['storeFile'])
                storePassword configuration['storePassword']
                keyAlias configuration['keyAlias']
                keyPassword configuration['keyPassword']
            } else {
                reportMissingSigningConfiguration('release')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    api 'com.google.firebase:firebase-firestore:11.8.0'
    api 'com.google.firebase:firebase-core:11.8.0'
}

apply plugin: 'com.google.gms.google-services'
